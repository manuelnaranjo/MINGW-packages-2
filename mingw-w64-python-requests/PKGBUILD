# Contributor: Naranjo Manuel <naranjo.manuel@gmail.com>

_realname=requests
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python2-${_realname}" "${MINGW_PACKAGE_PREFIX}-python3-${_realname}")
pkgver=2.11.1
pkgrevision=r1
pkgrel=2
pkgdesc='Requests is the only Non-GMO HTTP library for Python, safe for human consumption. (mingw-w64)'
arch=('any')
url='http://python-requests.org/'
license=('APACHEv2')
makedepends=("${MINGW_PACKAGE_PREFIX}-python3"
             "${MINGW_PACKAGE_PREFIX}-python2")
source=("https://github.com/kennethreitz/${_realname}/archive/v${pkgver}.zip")
sha256sums=('ce2b0c2ef5185a7f64962682cde7fa31ca79d4718d755904762dd2153dbafea3')
noextract=(v${pkgver}.zip)

prepare() {
  [[ -d ${srcdir}/${_realname}-${pkgver} ]] && rm -rf ${srcdir}/${_realname}-${pkgver}
  mkdir -p ${srcdir}
  cd ${srcdir}
  unzip v${pkgver}.zip || true

  cp -r ${_realname}-${pkgver} python2-build
  cp -r ${_realname}-${pkgver} python3-build
}

package_python3-requests() {
  depends=("${MINGW_PACKAGE_PREFIX}-python3")

  cd ${srcdir}/python3-build
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python3 setup.py install --prefix=${MINGW_PREFIX} \
    --root="${pkgdir}" --optimize=1
}

package_python2-requests() {
  depends=("${MINGW_PACKAGE_PREFIX}-python2")

  cd ${srcdir}/python2-build
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python2 setup.py install --prefix=${MINGW_PREFIX} \
    --root="${pkgdir}" --optimize=1
}

package_mingw-w64-i686-python2-requests() {
  package_python2-requests
}

package_mingw-w64-i686-python3-requests() {
  package_python3-requests
}

package_mingw-w64-x86_64-python2-requests() {
  package_python2-requests
}

package_mingw-w64-x86_64-python3-requests() {
  package_python3-requests
}
