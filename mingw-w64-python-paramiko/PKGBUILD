# Contributor: Naranjo Manuel <naranjo.manuel@gmail.com>

_realname=paramiko
pkgbase=mingw-w64-python-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-python2-${_realname}" "${MINGW_PACKAGE_PREFIX}-python3-${_realname}")
pkgver=2.0.1
pkgrel=1
pkgdesc='The leading native Python SSHv2 protocol library. (mingw-w64)'
arch=('any')
url="https://github.com/${_realname}/${_realname}"
license=('GPL')
makedepends=("${MINGW_PACKAGE_PREFIX}-python3"
             "${MINGW_PACKAGE_PREFIX}-python2")
source=("${url}/archive/${pkgver}.tar.gz")
sha256sums=('8a59d7085ed0adf8a6254e5b96c9f99249739ded1c7beddecf0c37cc28b3cca4')

prepare() {
  cp -r ${_realname}-${pkgver} python2-build
  cp -r ${_realname}-${pkgver} python3-build
}

package_python3-paramiko() {
  depends=("${MINGW_PACKAGE_PREFIX}-python3")

  cd ${srcdir}/python3-build
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python3 setup.py install --prefix=${MINGW_PREFIX} \
    --root="${pkgdir}" --optimize=1

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/python3-${_realname}/LICENSE"
}

package_python2-paramiko() {
  depends=("${MINGW_PACKAGE_PREFIX}-python2")

  cd ${srcdir}/python2-build
  MSYS2_ARG_CONV_EXCL="--prefix=;--install-scripts=;--install-platlib=" \
  ${MINGW_PREFIX}/bin/python2 setup.py install --prefix=${MINGW_PREFIX} \
    --root="${pkgdir}" --optimize=1

  install -Dm644 LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/python2-${_realname}/LICENSE"
}

package_mingw-w64-i686-python2-paramiko() {
  package_python2-paramiko
}

package_mingw-w64-i686-python3-paramiko() {
  package_python3-paramiko
}

package_mingw-w64-x86_64-python2-paramiko() {
  package_python2-paramiko
}

package_mingw-w64-x86_64-python3-paramiko() {
  package_python3-paramiko
}
