# Maintainer: Nazar Mishturak <nazar m x at gmail dot com>

_realname=libbotan110
pkgbase="mingw-w64-${_realname}"
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.10.13
pkgrel=1
arch=('any')
url='http://botan.randombit.net/'
pkgdesc='Botan is a C++ cryptography library released under the permissive Simplified BSD license. (mingw-w64)'
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
         "${MINGW_PACKAGE_PREFIX}-boost"
         "${MINGW_PACKAGE_PREFIX}-bzip2"
         "${MINGW_PACKAGE_PREFIX}-sqlite3"
         "${MINGW_PACKAGE_PREFIX}-zlib"
         "${MINGW_PACKAGE_PREFIX}-xz")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-python3"
             "${MINGW_PACKAGE_PREFIX}-python3-sphinx"
	     "${MINGW_PACKAGE_PREFIX}-python3-imagesize")
license=('BSD')
options=('strip' 'staticlibs' 'docs')
source=(http://botan.randombit.net/releases/Botan-${pkgver}.tgz)
sha256sums=('23ec973d4b4a4fe04f490d409e08ac5638afe3aa09acd7f520daaff38ba19b90')

prepare() {
  cd "${srcdir}/Botan-${pkgver}"
}

build() {
  rm -rf build-${MINGW_CHOST}
  cp -rf Botan-${pkgver} build-${MINGW_CHOST}
  cd build-${MINGW_CHOST}

  sed -i "s|/mingw|${MINGW_PREFIX}|g" src/build-data/os/mingw.txt

  local BUILD_TYPE_FLAGS=''
  if check_option 'debug' 'y'; then
    BUILD_TYPE_FLAGS='--with-debug-info'
  fi

  # Set prefix to mingw{32,64} not full path. Remove starting slash
  ${MINGW_PREFIX}/bin/python3 configure.py \
                              --disable-shared \
                              --os=mingw \
                              --cc=gcc \
                              --cpu=${CARCH} \
                              --prefix='$(DESTDIR)' \
                              ${BUILD_TYPE_FLAGS} \
                              --makefile-style=unix \
                              --link-method=copy \
                              --with-sphinx \
                              --with-boost \
                              --with-bzip2

    make
    make check
}

check() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  ./check.exe --test
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR="${pkgdir}${MINGW_PREFIX}" install
}
